# Maintainer: envolution
# Contributor: Mark Wells <contact at markwells dot dev>
# Contributor: mdaniels5757 <arch at mdaniels dot me>
# Contributor: ejiek <ejiek@mail.ru>
# Contributor: jskier <jay @jskier.com>
# shellcheck shell=bash disable=SC2034,SC2154
pkgname=keeper-commander
pkgver=17.2.9
pkgrel=1
epoch=1
pkgdesc="CLI, SDK and interactive shell for KeeperÂ® Password Manager."
arch=('any')
url="https://github.com/Keeper-Security/Commander"
license=('MIT')
depends=('python'
    'python-asciitree'
    'python-bcrypt'
    'python-colorama'
    'python-prompt_toolkit'
    'python-pycryptodomex'
    'python-pyperclip'
    'python-tabulate'
    'python-fido2'
    'python-requests'
    'keeper-secrets-manager-core>=16.6.0'
    'python-protobuf'
    'python-cryptography'
    'python-pydantic'
    'python-flask-limiter'
    'python-pyngrok'
    'python-dotenv'
    'python-psutil'
    'python-websockets'
    'python-textual'
    'python-flask'
    'python-certifi'
)
optdepends=(
    'python-fpdf2: PDF export support'
    'python-pysocks: SOCKS proxy support'
    'python-aiortc: WebRTC support'
    'google-crc32c: Cloud integration support'
    'python-pykeepass: KeePass support'
    'python-ldap3: LDAP support'
    'python-botocore: AWS support'
    'python-paramiko: SSH/SFTP support'
    'python-pexpect: Automation support'
    'python-google-auth: Google auth support'
    'python-boto3: AWS SDK support'
    'python-yaml: YAML support'
    'python-google-api-python-client: Google API support'
)
makedepends=(
    'python-installer'
    'python-build'
    'python-setuptools'
    'python-setuptools-scm'
)
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('ee278a3c356f2611c227c7f6da0300509935d304e738e6295839317dbe90d84f778c68fb7b757a188926cc5a2028b5b195c1d04d04becaa965fac489164983ed')

build() {
    cd "Commander-$pkgver"
    python -m build --wheel --no-isolation
}

package() {
    cd "Commander-$pkgver"
    python -m installer --destdir="$pkgdir" dist/*.whl
    install -D -m644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
# vim:set ts=2 sw=2 et:
