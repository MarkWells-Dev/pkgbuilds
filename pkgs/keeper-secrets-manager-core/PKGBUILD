# Maintainer: Mark Wells <contact at markwells dot dev>
# shellcheck shell=bash disable=SC2034,SC2154
pkgname=keeper-secrets-manager-core
pkgver=17.2.0
pkgrel=1
pkgdesc="Keeper Secrets Manager SDK core module"
arch=('any')
url="https://github.com/Keeper-Security/secrets-manager"
license=('MIT')
depends=(
    'python'
    'python-requests'
    'python-cryptography>=39.0.1'
    'python-importlib-metadata'
    'python-urllib3>=2.6.0'
)
makedepends=(
    'python-build'
    'python-installer'
    'python-setuptools'
)
source=("https://files.pythonhosted.org/packages/source/k/keeper_secrets_manager_core/keeper_secrets_manager_core-$pkgver.tar.gz")
sha512sums=('f4e0cd433d300e8759774a7628f2beb89197c7810abb947d1256fe96e982f15fd5412a5a3f066fe8227dffc6abc4674c12f19b3d11d3e6e4c448c2d1efe1ebe3')

build() {
    cd "keeper_secrets_manager_core-$pkgver"
    python -m build --wheel --no-isolation
}

package() {
    cd "keeper_secrets_manager_core-$pkgver"
    python -m installer --destdir="$pkgdir" dist/*.whl
}
# vim:set ts=2 sw=2 et:
